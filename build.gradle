plugins {
    id 'java'
}

group = 'com.pyjavabridge'

def resolveVersionFromGit = {
    def versionPattern = ~/^(\d+[A-Z])\s*-\s*.*$/
    def result = providers.exec {
        commandLine 'git', 'log', '-1', '--pretty=%s'
        ignoreExitValue = true
    }

    def lastSubject = result.standardOutput.asText.get().trim()

    if (lastSubject ==~ versionPattern) {
        def matcher = (lastSubject =~ versionPattern)
        return matcher[0][1]
    }

    return 'dev'
}

version = resolveVersionFromGit()

def paperVersion = '1.21.4-R0.1-SNAPSHOT'

dependencies {
    compileOnly "io.papermc.paper:paper-api:${paperVersion}"
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = 21
}

tasks.named('jar') {
    archiveFileName = "${project.name}-${project.version}.jar"
}

tasks.register('copyPluginJar', Copy) {
    dependsOn tasks.named('jar')
    from tasks.named('jar').flatMap { it.archiveFile }
    def pluginsDir = layout.projectDirectory.dir("server/plugins")
    into pluginsDir
    doFirst {
        pluginsDir.asFile.mkdirs()
        delete(fileTree(pluginsDir) {
            include "pyjavabridge-*.jar"
        })
    }
}

tasks.register('copyReleaseJar', Copy) {
    dependsOn tasks.named('jar')
    from tasks.named('jar').flatMap { it.archiveFile }
    into "${rootDir}/releases"
    doFirst {
        file("${rootDir}/releases").mkdirs()
    }
}

tasks.named('build') {
    finalizedBy tasks.named('copyPluginJar'), tasks.named('copyReleaseJar')
}
